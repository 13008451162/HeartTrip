def cfg = rootProject.ext
if (cfg.isModule) {
    //true表示当前处于组件独立开发模式中
    apply plugin: 'com.android.application'
} else {
    //false表示当前处于项目集成开发模式中
    apply plugin: 'com.android.library'
}

android {
    namespace 'com.xupt3g.homepageview'
    compileSdk cfg.android.compileSdkVersion

    defaultConfig {
        if (cfg.isModule) {
            //true表示当前处于组件独立开发模式中
            applicationId cfg.applicationId.homepageView   //com.xupt3g.homepageview
        }
        minSdk cfg.android.minSdkVersion
        targetSdk cfg.android.targetSdkVersion
        versionCode cfg.android.versionCode
        versionName cfg.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        ndk {
            // 设置支持的SO库架构（开发者可以根据需要，选择一个或多个平台的so）
            abiFilters "armeabi", "armeabi-v7a", "arm64-v8a", "x86","x86_64"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            if (cfg.isModule) {
                //true表示当前处于组件独立开发模式中
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //正式环境下屏蔽Debug包下的所有文件
                java{
                    exclude "**/debug/**"
                }
            }
        }
    }
}

dependencies {
    implementation project(':moduleBase:libBase')
    implementation project(':moduleBase:mylibrary1')
    implementation 'androidx.activity:activity:1.8.0'
    implementation 'androidx.core:core:1.12.0'
    annotationProcessor 'androidx.room:room-compiler:2.6.1'
    annotationProcessor cfg.libARouterCompiler
    annotationProcessor cfg.liblombok

    implementation 'com.android.support:support-v4:28.0.0' // 或者更新的版本
    implementation 'com.android.support:design:28.0.0' // 或者更新的版本

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}